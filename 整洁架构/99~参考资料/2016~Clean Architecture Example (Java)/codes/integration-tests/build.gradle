description = "Integration Tests for checking the entrypoints and dataproviders"

test {

    // make gradle print the test result for each test in the build (we like to see the acceptance tests running)
    testLogging {
        events "passed", "skipped", "failed"
    }

    // set the output folder for the acceptance tests that use yatspec, and print the full path after the test execution
    systemProperty "yatspec.output.dir", "build/reports/yatspec"
    doLast {
        println "==========================================================================================="
        println "Integration tests output: ${project.buildDir.absolutePath}/reports/yatspec/com/clean/example"
        println "==========================================================================================="
    }
}

// we want to run the integration tests after the acceptance tests, to follow the testing pyramid
test.mustRunAfter(":acceptance-tests:test")

// we must copy the h2-schema.sql script into the resources folder of this module in order for the tests
// to be able to create the in-memory database successfull when initialising the connection
task copyTestResources(type: Copy) {
    from "${project(':application:dataproviders').buildDir}/resources/main/h2-schema.sql"
    into "${buildDir}/resources/test"
}
processTestResources.dependsOn copyTestResources


dependencies {
    compile project(":application:configuration")
    compile libs.unit_tests
    compile libs.integration_test

    testCompile project(":acceptance-tests")
}